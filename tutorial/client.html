<script type="statebus">                          #Scripts with this tag are interpreted by statebus

dom.BODY = ->                                     #Define the react component that renders the dom body
  messages = fetch('/chat').messages or []        #Synchronize with the chat messages using statebus  
  DIV {},                                         #Define a div that displays the messages       
    for message in messages                       #For each message render its text  
      DIV(message.content)
    NEW_MESSAGE()                                 #A component for writing new messsages

                                                  #Defining the new message component here
dom.NEW_MESSAGE = ->
  new_message = fetch('new_message')              #Access local state for the new message
  DIV {},
    INPUT
      type: 'text'
      value: new_message.text                     #Display the new message in the input 
      onChange: (e) =>                            #When someone types, update the text in this input box
        new_message.text = e.target.value         #Save this local state and re-render the NEW_MESSAGE component
        save(new_message)

    BUTTON
      onClick: (e) =>                             #When someone clicks on this button, publish their message
        chat = fetch('/chat')                     #Get the chat messages from statebus
        chat.messages or= []                      #Add a new message to the chat and save it using statebus
        message = {
          key: "/message/#{random_string()}"         #Give the message its own state URL. Will be useful for us later.
          content: new_message.text
        }
        chat.messages.push( message )             #Add the new message to the chat history
        save(message)                             #Publish the new message and cause the body to re-render
        new_message.text = ''
        save(new_message)
      'Send'

random_string = -> Math.random().toString(36).substring(3)

</script><script src="https://stateb.us/client5.js" server="http://localhost:3005"></script> #This is the server we'll synchronize our state with.

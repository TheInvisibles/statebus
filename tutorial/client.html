<script type="statebus">                          # Scripts with this tag are interpreted by statebus

dom.BODY = ->                                     # Define the webpage with dom.BODY = ->
  messages = fetch('/chat').messages or []        # Get the current state of the chat!
  DIV {},
    for message in messages
      DIV(message.content)
    NEW_MESSAGE()

dom.NEW_MESSAGE = ->
  new_message = fetch('new_message')              # The state of the message as it is being written

  DIV {},
    INPUT
      type: 'text'
      value: new_message.text                     # Show the current state of the text in the box
      onChange: (e) =>                            # ...and when it changes:
        new_message.text = e.target.value         #    1) Update the state of the text
        save(new_message)                         #    2) And save the new value to the bus!

    BUTTON                                        # This button sends the message!
      onClick: (e) =>                             #
        chat = fetch('/chat')                     #    1) Take all messages
        chat.messages or= []                      #       ... initialize them if empty
                                                  #
        chat.messages.push({                      #    2) Add our new message!
          key: "/message/#{random_string()}"      #       ... with a new random key
          content: new_message.text               #
        })                                        #
        save(chat)                                #    3) Save our (now larger) list of messages!
                                                  #
        new_message.text = ''                     #    4) And clear out the new message box
        save(new_message)
      'Send'

random_string = -> Math.random().toString(36).substring(3)

</script><script src="https://stateb.us/client6.js"></script>
